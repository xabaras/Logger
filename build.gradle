plugins {
    id "com.jfrog.bintray" version "1.6"
}

group 'it.xabaras.android.logger'
version '1.3.5'

def androidSdkdir = System.properties['user.home'] + '/adt-bundle-mac/sdk/'
def androidSdktarget = 8

apply plugin: 'java'
apply plugin: "maven-publish"

sourceCompatibility = 1.6

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes(
                "Created-By": "Paolo Montalto",
                "Specification-Title": rootProject.name,
                "Implementation-Title": group,
                "Implementation-Version": version
        )
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile files(androidSdkdir + 'platforms/android-' + androidSdktarget + '/android.jar')
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray_user')
    key = properties.getProperty('bintray_api_key')
    publications = ['mavenStuff']
    pkg {
        repo = 'maven'
        name = 'Logger'
        desc = 'A simple class wrapping android logger introducing some utilities.'
        websiteUrl = 'https://github.com/xabaras/Logger'
        issueTrackerUrl = 'https://github.com/xabaras/Logger/issues'
        vcsUrl = 'https://github.com/xabaras/Logger.git'
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        githubRepo = 'xabaras/Logger' //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file

        version {
            name = '1.3.5'
            desc = 'Logger 1.3.5'
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty('bintray_gpg_password') //Optional. The passphrase for GPG signing'
            }
            vcsTag = 'v1.3.5'
            mavenCentralSync {
                sync = false //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = properties.getProperty('oss_user') //OSS user token
                password = properties.getProperty('oss_password') //OSS user password
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}

publishing {
    publications {
        mavenStuff(MavenPublication) {
            from components.java
            groupId group
            artifactId rootProject.name
            version version
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() +  {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name bintray.pkg.name
                    description bintray.pkg.desc
                    url bintray.pkg.websiteUrl
                    scm {
                        url bintray.pkg.vcsUrl
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'xabaras'
                            name 'Paolo Montalto'
                            email 'webmaster@xabaras.it'
                        }
                    }
                }
            }
        }
    }
}

